apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0-001-SNAPSHOT"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.smartnsoft:connectivitylistener:1.0.0-001-SNAPSHOT'
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    title = "LiveData implementation of the Connectivity Listener API"
    description "Javadoc for the LiveData implementation of the ConnectivityListener library"
    source = android.sourceSets.main.java.srcDirs

    options.charSet = "UTF-8"
    options.overview = "src/main/javadoc/overview.html"

    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

artifacts
{
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    licenses
    {
        license
        {
            name "MIT License (MIT)"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
}

publishing
{
    publications
    {
        ReleasePublication(MavenPublication)
        {
            artifact sourcesJar
            artifact javadocJar
            groupId "com.smartnsoft"
            artifactId "livedataconnectivitylistener"
            version android.defaultConfig.versionName

            pom.withXml
            {
                def root = asNode()
                root.appendNode("description", "An implementation using LiveData of the ConnectivityListener library.")
                root.appendNode("name", "livedataconnectivitylistener")
                root.appendNode("url", "https://github.com/smartnsoft/livedata-connectivity-listener")
                root.children().last() + pomConfig
            }
        }
    }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
    {
        pom.withXml
        {
            asNode().dependencies."*".findAll()
            {
                it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
                    dep.name == it.artifactId.text()
                }
            }.each
                {
                    it.scope*.value = "compile"
                }
        }
    }

bintray
    {
        publications = ["ReleasePublication"]

        pkg
        {
            repo = "maven"
            name = "livedataconnectivitylistener"
            userOrg = "smartnsoft"
            licenses = ["MIT"]
            vcsUrl = ""
            publish = true

            version
            {
                name = android.defaultConfig.versionName
                desc = "An implementation using LiveData of the ConnectivityListener library."
                released = new Date()
                vcsTag = "v" + android.defaultConfig.versionName
            }
        }
    }

uploadArchives
{
    repositories
    {
        mavenDeployer
        {
            repository(url: "https://api.bintray.com/maven/smartnsoft/maven/livedataconnectivitylistener/;publish=1")
            {
                authentication(userName: bintrayUsername, password: bintrayKey)
            }

            snapshotRepository(url: nexusSnapshotUrl)
            {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project
            {
                name "livedataconnectivitylistener"
                groupId "com.smartnsoft"
                artifactId "livedataconnectivitylistener"
                version android.defaultConfig.versionName
                packaging "aar"
            }
        }
    }
}