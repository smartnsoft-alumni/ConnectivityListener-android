apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

def moduleSuffix = "-livedata"
def moduleArtifactId = project.artifactId + moduleSuffix
def moduleLibraryName = project.libraryName + moduleSuffix

android {
  compileSdkVersion rootProject.compileSdkVersion


  defaultConfig {
    minSdkVersion rootProject.minSdkVersion
    targetSdkVersion rootProject.compileSdkVersion
    versionCode rootProject.versionCode
    versionName rootProject.liveDataVersionName
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    kotlinOptions.freeCompilerArgs += ['-module-name', "$groupId.$moduleArtifactId"]
  }

}

dependencies {
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}")
  implementation("com.android.support:appcompat-v7:28.0.0")
  api project(":library")
  //implementation("$groupId.$artifactId:1.0.0")
}
task sourcesJar(type: Jar) {
  archiveClassifier.set("sources")
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  enabled = false

  title = moduleLibraryName + " API"
  description "Javadoc for the " + moduleLibraryName
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = project.documentationReference

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set("javadoc")
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId project.groupId
      artifactId moduleArtifactId
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", project.liveDataDescription)
        root.appendNode("name", moduleLibraryName)
        root.appendNode("url", "https://github.com/smartnsoft/" + project.repositoryName)
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == moduleArtifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = moduleLibraryName
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/smartnsoft/" + project.repositoryName + ".git"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = project.liveDataDescription
      vcsTag = "v" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/" + moduleLibraryName + "/;publish=1")
      {
        authentication(userName: findProperty("bintrayUsername") ?: "", password: findProperty("bintrayKey") ?: "")
      }

      snapshotRepository(url: findProperty("nexusSnapshotUrl") ?: "")
      {
        authentication(userName: findProperty("nexusUsername") ?: "", password: findProperty("nexusPassword") ?: "")
      }

      pom.project
      {
        name moduleLibraryName
        groupId project.groupId
        artifactId moduleArtifactId
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}